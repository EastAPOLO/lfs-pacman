pkgname=htop
pkgver=2.0.2
pkgrel=1
pkgdesc="Interactive process viewer"
arch=('x86_64')
url="http://hisham.hm/htop/"
license=('GPL')
depends=('ncurses')
optdepends=('lsof: show files opened by a process'
            'strace: attach to a running process')
options=('!emptydirs')
source=("https://hisham.hm/$pkgname/releases/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('179be9dccb80cee0c5e1a1f58c8f72ce7b2328ede30fb71dcdf336539be2f487')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --enable-unicode \
      --enable-openvz \
      --enable-vserver \
      --enable-cgroup

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
